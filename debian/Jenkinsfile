#!groovy

// Aktuální verze Pipeline: https://github.com/VitexSoftware/BuildImages/blob/main/Test/Jenkinsfile-parael

String[] distributions = [
    'debian:bookworm',
    'debian:trixie',
    'debian:forky',
    'ubuntu:jammy',
    'ubuntu:noble'
]

String vendor = 'vitexsoftware'
String imagePrefix = 'multiflexi-'

properties([
    copyArtifactPermission('*')
])

node {
    ansiColor('xterm') {
        stage('SCM Checkout') {
            checkout scm
        }
    }
}

def branches = [:]

distributions.each { distro ->
    branches[distro] = {
        def (distroFamily, distroCode) = distro.split(':')
        def imageName = "${vendor}/${imagePrefix}${distroCode}:latest"
        def buildImage
        def artifacts = []
        def buildVer

        node {
            ansiColor('xterm') {
                stage("Checkout ${distro}") {
                    checkout scm
                    buildImage = docker.image(imageName)

                    sh 'git checkout debian/changelog'
                    buildVer = sh(
                        script: "dpkg-parsechangelog --show-field Version",
                        returnStdout: true
                    ).trim() + ".${env.BUILD_NUMBER}~${distroCode}"
                }

                stage("Build ${distro}") {
                    buildImage.inside('--ipc=host') {
                        sh """
                            dch -b -v ${buildVer} "${env.BUILD_TAG}"
                            sudo apt-get update --allow-releaseinfo-change
                            sudo chown -R jenkins:jenkins . ..
                            debuild-pbuilder -r'sudo -E' -i -us -uc -b
                            mkdir -p \$WORKSPACE/dist/debian/
                            rm -rf \$WORKSPACE/dist/debian/*
                            for deb in \$(awk '{print \$1}' debian/files); do
                              mv "../\$deb" \$WORKSPACE/dist/debian/
                            done
                        """
                        artifacts = sh(
                            script: "awk '{print \$1}' debian/files",
                            returnStdout: true
                        ).trim().split('\n')
                    }
                }

                stage("Test ${distro}") {
                    buildImage.inside('--ipc=host') {
                        def debconf_debug = 0
                        sh """
                            cd \$WORKSPACE/dist/debian/
                            dpkg-scanpackages . /dev/null > Packages
                            gzip -9c Packages > Packages.gz
                            cd \$WORKSPACE
                            echo "deb [trusted=yes] file://///\$WORKSPACE/dist/debian/ ./" | sudo tee /etc/apt/sources.list.d/local.list
                            sudo apt-get update --allow-releaseinfo-change
                        """
                        artifacts.each { deb_file ->
                            if (deb_file.endsWith('.deb')) {
                                def pkgName = deb_file.tokenize('/')[-1].replaceFirst(/_.*/, '')
                                def distroCodename = sh(
                                    script: "lsb_release -sc",
                                    returnStdout: true
                                ).trim()
                                echo "Installing ${pkgName} on ${distroCodename}"
                                sh """
                                    sudo DEBIAN_FRONTEND=noninteractive DEBCONF_DEBUG=${debconf_debug} \
                                         apt-get -y install ${pkgName} \
                                    || sudo apt-get -y -f install
                                """
                            }
                        }
                    }
                }

                stage("Archive artifacts ${distro}") {
                    buildImage.inside('--ipc=host') {
                        artifacts.each { deb_file ->
                            println "Archiving artifact: ${deb_file}"
                            archiveArtifacts artifacts: "dist/debian/${deb_file}"
                        }

                        sh '''
                            set -e
                            if [ -f debian/files ]; then
                              while read -r file _; do
                                [ -n "$file" ] || continue
                                rm -f "dist/debian/$file" || true
                                rm -f "../$file" || true
                                rm -f "$WORKSPACE/$file" || true
                              done < debian/files
                            fi
                        '''
                    }
                }

            }
        }
    }
}


parallel branches

node {
    stage('Publish to Aptly') {
        publishDebToAptly()
    }
}
